<!DOCTYPE html>
<html>
   <head>
<!--
   Ray Marching demo by Kevin Roast
   
   - Lots of code/ideas for this demo come from reading:
     http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
     http://www.pouet.net/topic.php?which=7931&page=1
     http://www.pouet.net/topic.php?which=7920&page=1
     http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
     https://www.shadertoy.com/
   
   If you want to understand this stuff read through the PDF and pouet.net links above!
-->
      <script src="scripts/dat.gui.min.js"></script>
      <script src="scripts/stats.min.js"></script>
      <script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
      <script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 lightDir;
uniform vec3 lightColour;
uniform float specular;
uniform float specularHardness;
uniform vec3 diffuse;
uniform float ambientFactor;
uniform bool ao;
uniform bool shadows;
uniform bool rotateWorld;
uniform bool antialias;
uniform bool dof;

// Symmetry group type.
uniform int Type;

// Barycentric coordinates for the principal node
uniform float U;
uniform float V;
uniform float W;

// Vertex radius
uniform float VRadius;

// Segments radius 
uniform float SRadius;

uniform bool displayFaces;
uniform bool displaySegments;
uniform bool displayVertices;
uniform bool textureSurface;

#define AO_SAMPLES 4
#define RAY_DEPTH 128
#define MAX_DEPTH 100.0
#define SHADOW_RAY_DEPTH 40
#define DISTANCE_MIN 0.001
#define PI 3.14159265
#define NoiseSize 128.0
#define NoiseRoughness 0.25
#define FLOOR_YPOS -0.9

vec3 nc,p,pab,pbc,pca;

const vec2 delta = vec2(DISTANCE_MIN, 0.);


float Hash(in float n)
{
    return fract(sin(n)*43758.5453123*PI);
}

float Noise(in vec2 x)
{
    vec2 p = floor(x);
    vec2 f = fract(x);
    f = f*f*(3.0-2.0*f);
    float n = p.x + p.y*57.0;
    float res = mix(mix( Hash(n+  0.0), Hash(n+  1.0),f.x),
                    mix( Hash(n+ 57.0), Hash(n+ 58.0),f.x),f.y);
    return res;
}

float Noise(in vec3 x)
{
    vec3 p = floor(x);
    vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);
    float n = p.x + p.y*57.0 + 113.0*p.z;
    return mix(mix(mix( Hash(n+  0.0), Hash(n+  1.0),f.x),
                   mix( Hash(n+ 57.0), Hash(n+ 58.0),f.x),f.y),
               mix(mix( Hash(n+113.0), Hash(n+114.0),f.x),
                   mix( Hash(n+270.0), Hash(n+271.0),f.x),f.y),f.z);
}

vec3 RotateY(vec3 p, float a)
{
   float c,s;
   vec3 q=p;
   c = cos(a);
   s = sin(a);
   p.x = c * q.x + s * q.z;
   p.z = -s * q.x + c * q.z;
   return p;
}

vec3 fold(vec3 pos)
{
   for (int i=0; i<5; i++)
   {
      pos.xy = abs(pos.xy);
      float t = -2.*min(0.,dot(pos,nc));
      pos += t*nc;
      if (i == Type) break;
   }
   return pos;
}

float D2Planes(vec3 pos)
{
   float d0=dot(pos,pab)-dot(pab,p);
   float d1=dot(pos,pbc)-dot(pbc,p);
   float d2=dot(pos,pca)-dot(pca,p);
   return max(max(d0,d1),d2);
}

float D2Segments(vec3 pos)
{
   pos -= p;
   float dla = length(pos-min(0.,pos.x)*vec3(1.,0.,0.));
   float dlb = length(pos-min(0.,pos.y)*vec3(0.,1.,0.));
   float dlc = length(pos-min(0.,dot(pos,nc))*nc);
   return min(min(dla,dlb),dlc)-SRadius;//max(max(dla,dlb),max(dlc,dlp))-SRadius;
}

float D2Vertices(vec3 pos)
{
   return length(pos-p)-VRadius;
}

// Distance estimator from here: (Knighty)
// https://github.com/Syntopia/Fragmentarium/blob/master/Fragmentarium-Source/Examples/Knighty%20Collection/Fold%20and%20Cut%20Polyhedra%20-%20Original.frag
float Dist(vec3 pos)
{
   if (rotateWorld) pos = RotateY(pos, sin(time*0.1)*PI);
   
   // copy position before it is folded (duplicates across planes!)
   vec3 p = pos;
   
   // fold and render display options
   pos = fold(pos);
   float d = 10000.0;
   if (displayVertices) d = min(d,D2Vertices(pos));
   if (displaySegments) d = min(d,D2Segments(pos));
   
   // do not display texture on the panel faces
   bool ts = textureSurface;
   if (displayFaces)
   {
      float dp = min(d,D2Planes(pos));
      if (dp < d)
      {
         ts = false;
         d = dp;
      }
   }
   
   // floor plane
   d = min(d, dot(p-vec3(0.,FLOOR_YPOS,0.), vec3(0.,1.,0.)));
   float flr = p.y-FLOOR_YPOS;
   if (d < flr)
   {
      if (ts)
      {
         d = d + Noise(p*NoiseSize)*NoiseRoughness/NoiseSize;
      }
   }
   /*else if (ts)
   {
      d = flr + Noise(p.xz*NoiseSize)*NoiseRoughness/NoiseSize;
   }
   else
   {
      d = flr;
   }*/
   return d;
}

// Based on original by IQ - optimized to remove a divide
float CalcAO(vec3 p, vec3 n)
{
   float r = 0.0;
   float w = 1.0;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float d0 = float(i) * 0.3;
      r += w * (d0 - Dist(p + n * d0));
      w *= 0.5;
   }
   return 1.0 - clamp(r,0.0,1.0);
}

// Based on original code by IQ
float SoftShadow(vec3 ro, vec3 rd, float k)
{
   float res = 1.0;
   float t = 0.05;         // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
   for (int i=0; i<SHADOW_RAY_DEPTH; i++)
   {
      float h = Dist(ro + rd * t);
      res = min(res, k*h/t);
      t += h;
      if (t > 10.0) break; // max-t
   }
   return clamp(res,0.0,1.0);
}

vec3 GetNormal(vec3 pos)
{
   vec3 n;
   n.x = Dist( pos + delta.xyy ) - Dist( pos - delta.xyy );
   n.y = Dist( pos + delta.yxy ) - Dist( pos - delta.yxy );
   n.z = Dist( pos + delta.yyx ) - Dist( pos - delta.yyx );
   
   return normalize(n);
}

// Based on a shading method by Ben Weston. Added AO and SoftShadows to original.
vec4 Shading(vec3 pos, vec3 rd, vec3 norm)
{
   // simple pos test on pos.y for floor (see Dist() above) - different colour and no spec for floor
   if (pos.y > FLOOR_YPOS+DISTANCE_MIN)
   {
      vec3 light = lightColour * max(0.0, dot(norm, lightDir));
      vec3 heading = normalize(-rd + lightDir);
      float spec = pow(max(0.0, dot(heading, norm)), specularHardness);
      light = (diffuse * light) + (spec * specular * lightColour);
      if (shadows) light *= SoftShadow(pos, lightDir, 64.0);   // harder edged shadows on object
      if (ao) light += CalcAO(pos, norm) * ambientFactor;
      return vec4(light, 1.0);
   }
   else
   {
      vec3 light = vec3(0.6,0.8,0.3) * max(0.0, dot(norm, lightDir));
      if (shadows) light *= SoftShadow(pos, lightDir, 32.0);   // softer edged shadows on floor
      if (ao) light += CalcAO(pos, norm) * ambientFactor;
      return vec4(light, 1.0);
   }
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
   float sunAmount = max(dot(rd, lightDir), 0.0);
   float v = pow(1.0 - max(rd.y,0.0),6.);
   vec3 sky = mix(vec3(.1, .2, .3), vec3(.32, .32, .32), v);
   sky += lightColour * sunAmount * sunAmount * .25 + lightColour * min(pow(sunAmount, 800.0)*1.5, .3);
   return sky;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

vec4 March(vec3 ro, vec3 rd)
{
   float t = 0.0;
   float d = 1.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      vec3 p = ro + rd * t;
      d = Dist(p);
      if (abs(d) < DISTANCE_MIN)
      {
         return vec4(p, 1.0);
      }
      t += d;
      if (t >= MAX_DEPTH) break;
   }
   return vec4(0.0);
}

void main()
{
   const int ANTIALIAS_SAMPLES = 4;
   const int DOF_SAMPLES = 16;
   
   // precalc - once...
   float cospin=cos(PI/float(Type)), scospin=sqrt(0.75-cospin*cospin);
   nc=vec3(-0.5,-cospin,scospin);
   pab=vec3(0.,0.,1.);
   pbc=normalize(vec3(scospin,0.,0.5));
   pca=normalize(vec3(0.,scospin,cospin));
   p=normalize((U*pab+V*pbc+W*pca));
   
   vec4 res = vec4(0.0);
   
   if (antialias)
   {
      float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
      float ang = d_ang * 0.33333;
      float r = 0.3;
      for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
      {
         vec2 p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
         vec3 ro = cameraPos;
         vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
         vec4 _res = March(ro, rd);
         // TODO: If floor, optimize normal generation i.e. already known!!
         if (_res.a == 1.0) res.xyz += Shading(_res.xyz, rd, GetNormal(_res.xyz)).xyz;
         else res.xyz += Sky(rd);
         ang += d_ang;
      }
      res.xyz /= float(ANTIALIAS_SAMPLES);
   }
   /*else if (dof)
   {
      vec2 p = gl_FragCoord.xy / resolution.xy;
      vec3 ro = cameraPos;
      vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
      vec4 _res = March(ro, rd);
      
      float d_ang = 2.*PI / float(DOF_SAMPLES);
      float ang = d_ang * 0.33333;
      // cheap DOF!
      float r = abs(cameraLookat.z - _res.z) * 1.0;
      for (int i = 0; i < DOF_SAMPLES; i++)
      {
         p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
         ro = cameraPos;
         rd = normalize(GetRay(cameraLookat-cameraPos, p));
         _res = March(ro, rd);
         if (_res.a == 1.0) res.xyz += Shading(_res.xyz, rd, GetNormal(_res.xyz)).xyz;
         else res.xyz += Sky(rd);
         ang += d_ang;
      }
      res.xyz /= float(DOF_SAMPLES);
   }*/
   else
   {
      vec2 p = gl_FragCoord.xy / resolution.xy;
      vec3 ro = cameraPos;
      vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
      res = March(ro, rd);
      if (res.a == 1.0) res.xyz = Shading(res.xyz, rd, GetNormal(res.xyz)).xyz;
      else res.xyz = Sky(rd);
   }
   
   gl_FragColor = vec4(res.xyz, 1.0);
}
      </script>
      
      <script type="text/javascript">
var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
                            window.mozRequestAnimationFrame || window.msRequestAnimationFrame || 
                            function(c) {window.setTimeout(c, 15)};
var config = {
   camera: {
      x: 2.0, y: 2.0, z: 1.0
   },
   lookat: {
      x: 0.0, y: 0.0, z: 0.0
   },
   lightDir: {
      x: -7.0, y: 6.5, z: 0.5
   },
   lightColour: {
      r: 0.6, g: 0.7, b: 0.8
   },
   surface: {
      specular: 2.0,
      specularHardness: 16.0,
      diffuse: 0.35,
      ambientFactor: 0.35
   },
   global: {
      ao: true,
      shadows: true,
      rotateWorld: true,
      antialias: "None"// None|Classic|DOF
   },
   polyhedra: {
      type: 4,
      u: 0.5,
      v: 0.37,
      w: 0.5,
      vradius: 0.066,
      sradius: 0.050,
      displayFaces: false,
      displaySegments: true,
      displayVertices: true,
      textureSurface: true
   }
};

var pause = false;
var stats = new Stats();
var aspect, gl;
function init()
{
   document.addEventListener('keydown', function(e) {
      switch (e.keyCode)
      {
         case 27: // ESC
            pause = !pause;
            break;
      }
   }, false);

   // add GUI controls
   var mobile = (navigator.userAgent.indexOf("Android") !== -1);
   var gui = new dat.GUI();
   var panel = gui.addFolder('Camera Position');
   panel.add(config.camera, "x").min(-16.0).max(16.0).step(0.1);
   panel.add(config.camera, "y").min(-16.0).max(16.0).step(0.1);
   panel.add(config.camera, "z").min(-64.0).max(64.0).step(0.1);
   panel = gui.addFolder('Camera LookAt');
   panel.add(config.lookat, "x").min(-16.0).max(16.0).step(0.1);
   panel.add(config.lookat, "y").min(-16.0).max(16.0).step(0.1);
   panel.add(config.lookat, "z").min(-16.0).max(16.0).step(0.1);
   panel = gui.addFolder('Light Direction');
   panel.add(config.lightDir, "x").min(-16.0).max(16.0).step(0.1);
   panel.add(config.lightDir, "y").min(-16.0).max(16.0).step(0.1);
   panel.add(config.lightDir, "z").min(-16.0).max(16.0).step(0.1);
   panel = gui.addFolder('Light Colour');
   panel.add(config.lightColour, "r").min(0.0).max(3.0).step(0.1);
   panel.add(config.lightColour, "g").min(0.0).max(3.0).step(0.1);
   panel.add(config.lightColour, "b").min(0.0).max(3.0).step(0.1);
   panel = gui.addFolder('Surface');
   panel.add(config.surface, "specular").min(0).max(64);
   panel.add(config.surface, "specularHardness").min(16).max(1024).step(16);
   panel.add(config.surface, "diffuse").min(0).max(1).step(0.05);
   panel.add(config.surface, "ambientFactor").min(0).max(1).step(0.05);
   panel = gui.addFolder('Global');
   panel.add(config.global, "antialias", ["None", "Classic"]).name("Anti Alias");   //, "DOF"
   panel.add(config.global, "shadows").name("Shadows");
   panel.add(config.global, "ao").name("Ambient Occlusion");
   panel.add(config.global, "rotateWorld").name("Rotate World");
   if (!mobile) panel.open();
   panel = gui.addFolder('Polyhedra');
   panel.add(config.polyhedra, "type").min(1).max(5).step(1);
   panel.add(config.polyhedra, "u").min(0).max(1).step(0.01);
   panel.add(config.polyhedra, "v").min(0).max(1).step(0.01);
   panel.add(config.polyhedra, "w").min(0).max(1).step(0.01);
   panel.add(config.polyhedra, "vradius").min(0).max(0.1);
   panel.add(config.polyhedra, "sradius").min(0).max(0.05);
   panel.add(config.polyhedra, "displayFaces").name("Display Faces");
   panel.add(config.polyhedra, "displaySegments").name("Display Segments");
   panel.add(config.polyhedra, "displayVertices").name("Display Vertices");
   panel.add(config.polyhedra, "textureSurface").name("Textured Edges");
   if (!mobile) panel.open();
   
   document.body.appendChild(stats.domElement);
   
   // create webgl context on the canvas element
   var canvas = document.getElementById("canvas");
   aspect = canvas.width / canvas.height;
   try
   {
      gl = canvas.getContext("experimental-webgl");
   }
   catch (e)
   {
      document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
      return;
   }
   gl.viewport(0, 0, canvas.width, canvas.height);
   gl.clearColor(0, 0, 0, 1);
   gl.clear(gl.COLOR_BUFFER_BIT);
   
   // get the vertex and fragment shader source
   var v = document.getElementById("vertex").firstChild.nodeValue;
   var f = document.getElementById("fragment").firstChild.nodeValue;
   
   // compile and link the shaders
   var vs = gl.createShader(gl.VERTEX_SHADER);
   gl.shaderSource(vs, v);
   gl.compileShader(vs);
   
   var fs = gl.createShader(gl.FRAGMENT_SHADER);
   gl.shaderSource(fs, f);
   gl.compileShader(fs);
   
   var program = gl.createProgram();
   gl.attachShader(program, vs);
   gl.attachShader(program, fs);
   gl.linkProgram(program);
   
   // debug shader compile status
   var error = false;
   if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(vs));
   }
   
   if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(fs));
   }
   
   if (!gl.getProgramParameter(program, gl.LINK_STATUS))
   {
      error = true;
      console.log(gl.getProgramInfoLog(program));
   }
   if (error) return;
   
   var firstTime = Date.now();
   (f = function() {
      if (!pause)
      {
         stats.begin();
         
         // create vertices to fill the canvas with a single quad 
         var vertices = new Float32Array(
            [
               -1, 1*aspect, 1, 1*aspect, 1, -1*aspect,
               -1, 1*aspect, 1, -1*aspect, -1, -1*aspect
            ]);
         
         var vbuffer = gl.createBuffer();
         gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
         gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);
         
         var triCount = 2,
             numItems = vertices.length / triCount;
         
         gl.useProgram(program);
         
         var time = (Date.now() - firstTime) / 1000.0;
         program.time = gl.getUniformLocation(program, "time");
         gl.uniform1f(program.time, time);
         
         program.resolution = gl.getUniformLocation(program, "resolution");
         gl.uniform2f(program.resolution, canvas.width, canvas.height);
         
         program.cameraPos = gl.getUniformLocation(program, "cameraPos");
         gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);
         
         program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
         gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);
         
         program.lightDir = gl.getUniformLocation(program, "lightDir");
         // pre normalise light dir
         var x = config.lightDir.x, y = config.lightDir.y, z = config.lightDir.z;
         var len = x*x + y*y + z*z;
         len = 1.0 / Math.sqrt(len);
         gl.uniform3f(program.lightDir, config.lightDir.x*len, config.lightDir.y*len, config.lightDir.z*len);
         
         program.lightColour = gl.getUniformLocation(program, "lightColour");
         gl.uniform3f(program.lightColour, config.lightColour.r, config.lightColour.g, config.lightColour.b);
         
         program.specular = gl.getUniformLocation(program, "specular");
         gl.uniform1f(program.specular, config.surface.specular);
         
         program.specularHardness = gl.getUniformLocation(program, "specularHardness");
         gl.uniform1f(program.specularHardness, config.surface.specularHardness);
         
         program.diffuse = gl.getUniformLocation(program, "diffuse");
         gl.uniform3f(program.diffuse, config.surface.diffuse,config.surface.diffuse,config.surface.diffuse);
         
         program.ambientFactor = gl.getUniformLocation(program, "ambientFactor");
         gl.uniform1f(program.ambientFactor, config.surface.ambientFactor);
         
         program.rotateWorld = gl.getUniformLocation(program, "rotateWorld");
         gl.uniform1f(program.rotateWorld, config.global.rotateWorld);
         
         program.ao = gl.getUniformLocation(program, "ao");
         gl.uniform1f(program.ao, config.global.ao);
         
         program.shadows = gl.getUniformLocation(program, "shadows");
         gl.uniform1f(program.shadows, config.global.shadows);
         
         program.antialias = gl.getUniformLocation(program, "antialias");
         gl.uniform1f(program.antialias, (config.global.antialias === "Classic"));
         program.dof = gl.getUniformLocation(program, "dof");
         gl.uniform1f(program.dof, (config.global.antialias === "DOF"));
         
         program.Type = gl.getUniformLocation(program, "Type");
         gl.uniform1i(program.Type, config.polyhedra.type);
         
         program.U = gl.getUniformLocation(program, "U");
         gl.uniform1f(program.U, config.polyhedra.u);
         
         program.V = gl.getUniformLocation(program, "V");
         gl.uniform1f(program.V, config.polyhedra.v);
         
         program.W = gl.getUniformLocation(program, "W");
         gl.uniform1f(program.W, config.polyhedra.w);
         
         program.VRadius= gl.getUniformLocation(program, "VRadius");
         gl.uniform1f(program.VRadius, config.polyhedra.vradius);
         
         program.SRadius= gl.getUniformLocation(program, "SRadius");
         gl.uniform1f(program.SRadius, config.polyhedra.sradius);
         
         program.displayFaces = gl.getUniformLocation(program, "displayFaces");
         gl.uniform1f(program.displayFaces, config.polyhedra.displayFaces);
         program.displaySegments = gl.getUniformLocation(program, "displaySegments");
         gl.uniform1f(program.displaySegments, config.polyhedra.displaySegments);
         program.displayVertices = gl.getUniformLocation(program, "displayVertices");
         gl.uniform1f(program.displayVertices, config.polyhedra.displayVertices);
         
         program.textureSurface = gl.getUniformLocation(program, "textureSurface");
         gl.uniform1f(program.textureSurface, config.polyhedra.textureSurface);
         
         program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
         gl.enableVertexAttribArray(program.aVertexPosition);
         gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);
         
         gl.drawArrays(gl.TRIANGLES, 0, numItems);
         
         stats.end();
         //pause = true;
      }
      requestAnimationFrame(f);
   })();
}
      </script>
      <script src="scripts/utils.js"></script>
      <link rel="stylesheet" href="../../reset.css">
      <link rel="stylesheet" href="./page.css">
      <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
   </head>
   <body onload="init()">
      <canvas id="canvas" width="512" height="512"></canvas>
      <a href="#" id="fullscreen">Toggle Full Screen</a>
      <a href="." id="back">Show me more awesome demos!</a>
   </body>
</html>
<!DOCTYPE html>
<html>
   <head>
<!--
   Ray Marching demo by Kevin Roast
   
   - Lots of code/ideas for this demo come from reading:
     http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
     http://www.pouet.net/topic.php?which=7931&page=1
     http://www.pouet.net/topic.php?which=7920&page=1
     http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
     https://www.shadertoy.com/
   
   If you want to understand this stuff read through the PDF and pouet.net links above!
-->
      <script src="scripts/dat.gui.min.js"></script>
      <script src="scripts/stats.min.js"></script>
      <script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
      <script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 lightDir;
uniform vec3 lightColour;
uniform vec3 sssColour;
uniform vec3 diffuse;
uniform float ambientFactor;
uniform bool ao;
uniform bool sss;
uniform bool shadows;
uniform bool postEffects;
uniform bool rotateWorld;
uniform bool moveCamera;
uniform bool antialias;

#define PI 3.14159265
#define GAMMA 0.8
#define CONTRAST 1.1
#define SATURATION 1.2
#define BRIGHTNESS 1.2
#define AO_SAMPLES 5
#define RAY_DEPTH 200
#define MAX_DEPTH 200.0
#define SHADOW_RAY_DEPTH 16
#define DISTANCE_MIN 0.003

const vec2 delta = vec2(DISTANCE_MIN, 0.);


vec3 RotateZ(vec3 p, float a)
{
   float c,s;
   vec3 q=p;
   c = cos(a);
   s = sin(a);
   p.x = c * q.x - s * q.y;
   p.y = s * q.x + c * q.y;
   return p;
}

float HexPrism(vec3 p, vec2 h)
{
   vec3 q = abs(p);
   return max(q.y-h.y,max(q.x+q.z*0.57735,q.z*1.1547)-h.x);
}

float Plane(vec3 p, vec3 n)
{
   return dot(p, n);
}

vec3 ReplicateXZ(vec3 p, vec3 c)
{
   return vec3(mod(p.x, c.x) - 0.5 * c.x, p.y, mod(p.z, c.z) - 0.5 * c.z);
}

float Dist(vec3 pos)
{
   if (rotateWorld) pos = RotateZ(pos, sin(time)*0.25);
   vec3 q1 = ReplicateXZ(pos, vec3(3.5,0.,2.));
   vec3 q2 = ReplicateXZ(pos + vec3(3.5/2.0,0.,1.), vec3(3.5,0.,2.));
   return
      min(
         // ground plane - offset in the Y axis
         Plane(pos-vec3(0.,-0.5,0.), vec3(0.,1.,0.)),
         min(
            // subtract one hex prism from another to make a hollow shape
            max(
               HexPrism(q1, vec2(1.0,0.5)),
               -HexPrism(q1-vec3(0.,0.35,0.), vec2(0.7,0.4))
            ),
            // TODO: animate!!
            min(
               HexPrism(q2, vec2(1.0,0.5)),
               HexPrism(q2-vec3(0.,0.35,0.), vec2(0.7,0.4))
            )
         )
      );
}

// Based on original by IQ - optimized to remove a divide
float CalcAO(vec3 p, vec3 n)
{
   float r = 0.0;
   float w = 1.0;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float d0 = float(i) * 0.2; // 1.0/5.0
      r += w * (d0 - Dist(p + n * d0));
      w *= 0.5;
   }
   return 1.0 - r;
}

float CalcSSS(vec3 ro, vec3 rd)
{
   float total = 0.0;
   float weight = 0.5;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float delta = pow(float(i), 2.5) * 0.001 * 64.0;
      total += -weight * min(0.0, Dist(ro+rd * delta));
      weight *= 0.5;
   }
   return total;
}

// Based on original code by IQ
float SoftShadow(vec3 ro, vec3 rd, float k)
{
   float res = 1.0;
   float t = 0.05;         // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
   for (int i=0; i<SHADOW_RAY_DEPTH; i++)
   {
      float h = Dist(ro + rd * t);
      res = min(res, k*h/t);
      t += h;
      if (t > 10.0) break; // max-t
   }
   return clamp(res, 0.0, 1.0);
}

vec3 GetNormal(vec3 pos, float s)
{
   vec3 n;
   //n.x = Dist( pos + delta.xyy ) - Dist( pos - delta.xyy );
   //n.y = Dist( pos + delta.yxy ) - Dist( pos - delta.yxy );
   //n.z = Dist( pos + delta.yyx ) - Dist( pos - delta.yyx );
   
   // optimized normal calculation - based on final distance to surface see 704 demo
   n.x = s - Dist(pos - delta.xyy);
   n.y = s - Dist(pos - delta.yxy);
   n.z = s - Dist(pos - delta.yyx);
   
   return normalize(n);
}

// Lighting and shading - AO, Shadows, SSS.
vec4 Shading(vec3 pos, vec3 rd, vec3 norm)
{
   vec3 light = lightColour * max(0.0, dot(norm, lightDir));
   vec3 heading = normalize(-rd + lightDir);
   light = (diffuse * light);
   if (shadows) light *= SoftShadow(pos, lightDir, 16.0);
   if (sss) light = mix(light, sssColour, CalcSSS(pos, rd));
   if (ao) light += CalcAO(pos, norm) * ambientFactor;
   return vec4(light, 1.0);
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
   float sunAmount = max(dot(rd, lightDir), 0.0);
   float v = pow(1.0 - max(rd.y,0.0),6.);
   vec3 sky = mix(vec3(.1, .2, .3), vec3(.32, .32, .32), v);
   sky += lightColour * sunAmount * sunAmount * .25 + lightColour * min(pow(sunAmount, 800.0)*1.5, .3);
   return sky;
}

// Original method by David Hoskins
vec3 PostEffects(vec3 rgb, vec2 xy)
{
	rgb = pow(rgb, vec3(GAMMA));
	rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
	rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 );	
	return rgb;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

vec4 March(vec3 ro, vec3 rd)
{
   float t = 1.0;
   float d = 0.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      vec3 p = ro + rd * t;
      d = Dist(p);
      if (d < DISTANCE_MIN)
      {
         return vec4(p, d);
      }
      t += d;
      if (t >= MAX_DEPTH) break;
   }
   return vec4(0.0);
}

void main()
{
   const int ANTIALIAS_SAMPLES = 4;
   //const int DOF_SAMPLES = 16;
   
   //const bool dof = false;

   vec3 off = vec3(0.0);
   if (moveCamera) off.z -= time*5.0;
   
   vec4 res = vec4(0.0);
   
   if (antialias)
   {
      vec2 p;
      float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
      float ang = d_ang * 0.33333;
      float r = 0.3;
      for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
      {
         p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
         vec3 ro = cameraPos + off;
         vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
         vec4 _res = March(ro, rd);
         if (_res.a != 0.0) res.xyz += Shading(_res.xyz, rd, GetNormal(_res.xyz, _res.a)).xyz;
         else res.xyz += Sky(rd);
         ang += d_ang;
      }
      res.xyz /= float(ANTIALIAS_SAMPLES);
      if (postEffects) res.xyz = PostEffects(res.xyz, p);
   }
   /*else if (dof)
   {
      vec2 p = gl_FragCoord.xy / resolution.xy;
      vec3 ro = cameraPos + off;
      vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
      vec4 _res = March(ro, rd);
      
      float d_ang = 2.*PI / float(DOF_SAMPLES);
      float ang = d_ang * 0.33333;
      // cheap DOF!
      float r = abs(cameraLookat.z - _res.z) * .1;
      for (int i = 0; i < DOF_SAMPLES; i++)
      {
         p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
         ro = cameraPos + off;
         rd = normalize(GetRay(cameraLookat-cameraPos, p));
         _res = March(ro, rd);
         if (_res.a != 0.0) res.xyz += Shading(_res.xyz, rd, GetNormal(_res.xyz, _res.a)).xyz;
         else res.xyz += Sky(rd);
         ang += d_ang;
      }
      res.xyz /= float(DOF_SAMPLES);
      if (postEffects) res.xyz = PostEffects(res.xyz, p);
   }*/
   else
   {
      vec2 p = gl_FragCoord.xy / resolution.xy;
      vec3 ro = cameraPos + off;
      vec3 rd = normalize(GetRay(cameraLookat-cameraPos, p));
      res = March(ro, rd);
      if (res.a != 0.0) res.xyz = Shading(res.xyz, rd, GetNormal(res.xyz, res.a)).xyz;
      else res.xyz = Sky(rd);
      if (postEffects) res.xyz = PostEffects(res.xyz, p);
   }
   
   gl_FragColor = vec4(res.rgb, 1.0);
}
      </script>
      
      <script type="text/javascript">
var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
                            window.mozRequestAnimationFrame || window.msRequestAnimationFrame || 
                            function(c) {window.setTimeout(c, 15)};
var config = {
   camera: {
      x: 7.5, y: 10.0, z: 0.0
   },
   lookat: {
      x: 0.0, y: -100.0, z: -100.0 
   },
   lightDir: {
      x: 10.0, y: 6.0, z: 2.0
   },
   lightColour: {
      r: 0.0, g: 0.8, b: 2.0
   },
   sssColour: {
      r: 1.1, g: 1.5, b: 2.2
   },
   surface: {
      diffuse: 0.25,
      ambientFactor: 0.25
   },
   global: {
      ao: true,
      shadows: true,
      sss: true,
      postEffects: true,
      antialias: "None",// None|Classic|DOF
      rotateWorld: true,
      moveCamera: true
   }
};

var aspect, gl;
function init()
{
   var pause = false;
   document.addEventListener('keydown', function(e) {
      switch (e.keyCode)
      {
         case 27: // ESC
            pause = !pause;
            break;
      }
   }, false);

   // add GUI controls
   var mobile = (navigator.userAgent.indexOf("Android") !== -1);
   var gui = new dat.GUI();
   var panel = gui.addFolder('Camera Position');
   panel.add(config.camera, "x").min(-256).max(256).step(0.1);
   panel.add(config.camera, "y").min(-256).max(256).step(0.1);
   panel.add(config.camera, "z").min(-256).max(256).step(0.1);
   panel = gui.addFolder('Camera LookAt');
   panel.add(config.lookat, "x").min(-256).max(256).step(0.1);
   panel.add(config.lookat, "y").min(-256).max(256).step(0.1);
   panel.add(config.lookat, "z").min(-256).max(256).step(0.1);
   panel = gui.addFolder('Light Direction');
   panel.add(config.lightDir, "x").min(-100).max(100).step(0.1);
   panel.add(config.lightDir, "y").min(-100).max(100).step(0.1);
   panel.add(config.lightDir, "z").min(-100).max(100).step(0.1);
   panel = gui.addFolder('Light Colour');
   panel.add(config.lightColour, "r").min(0).max(3).step(0.1);
   panel.add(config.lightColour, "g").min(0).max(3).step(0.1);
   panel.add(config.lightColour, "b").min(0).max(3).step(0.1);
   if (!mobile) panel.open();
   panel = gui.addFolder('SSS Colour');
   panel.add(config.sssColour, "r").min(0).max(3).step(0.1);
   panel.add(config.sssColour, "g").min(0).max(3).step(0.1);
   panel.add(config.sssColour, "b").min(0).max(3).step(0.1);
   if (!mobile) panel.open();
   panel = gui.addFolder('Surface');
   panel.add(config.surface, "diffuse").min(0).max(1).step(0.05);
   panel.add(config.surface, "ambientFactor").min(0).max(1).step(0.05);
   if (!mobile) panel.open();
   panel = gui.addFolder('Global');
   panel.add(config.global, "antialias", ["None", "Classic"]).name("Anti Alias");
   panel.add(config.global, "shadows").name("Shadows");
   panel.add(config.global, "ao").name("Ambient Occlusion");
   panel.add(config.global, "sss").name("Sub-surface Scattering");
   panel.add(config.global, "postEffects").name("Post Effects");
   panel.add(config.global, "rotateWorld").name("Rotate World");
   panel.add(config.global, "moveCamera").name("Move Camera");
   if (!mobile) panel.open();
   
   var stats = new Stats();
   document.body.appendChild(stats.domElement);
   
   // create webgl context on the canvas element
   var canvas = document.getElementById("canvas");
   aspect = canvas.width / canvas.height;
   try
   {
      gl = canvas.getContext("experimental-webgl");
   }
   catch (e)
   {
      document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
      return;
   }
   gl.viewport(0, 0, canvas.width, canvas.height);
   gl.clearColor(0, 0, 0, 1);
   gl.clear(gl.COLOR_BUFFER_BIT);
   
   // get the vertex and fragment shader source
   var v = document.getElementById("vertex").firstChild.nodeValue;
   var f = document.getElementById("fragment").firstChild.nodeValue;
   
   // compile and link the shaders
   var vs = gl.createShader(gl.VERTEX_SHADER);
   gl.shaderSource(vs, v);
   gl.compileShader(vs);
   
   var fs = gl.createShader(gl.FRAGMENT_SHADER);
   gl.shaderSource(fs, f);
   gl.compileShader(fs);
   
   var program = gl.createProgram();
   gl.attachShader(program, vs);
   gl.attachShader(program, fs);
   gl.linkProgram(program);
   
   // debug shader compile status
   var error = false;
   if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(vs));
   }
   
   if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(fs));
   }
   
   if (!gl.getProgramParameter(program, gl.LINK_STATUS))
   {
      error = true;
      console.log(gl.getProgramInfoLog(program));
   }
   if (error) return;
   
   var firstTime = Date.now();
   (f = function() {
      if (!pause)
      {
         stats.begin();
         
         // create vertices to fill the canvas with a single quad 
         var vertices = new Float32Array(
            [
               -1, 1*aspect, 1, 1*aspect, 1, -1*aspect,
               -1, 1*aspect, 1, -1*aspect, -1, -1*aspect
            ]);
         
         var vbuffer = gl.createBuffer();
         gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
         gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);
         
         var triCount = 2,
             numItems = vertices.length / triCount;
         
         gl.useProgram(program);
         
         var time = (Date.now() - firstTime) / 1000.0;
         program.time = gl.getUniformLocation(program, "time");
         gl.uniform1f(program.time, time);
         
         program.resolution = gl.getUniformLocation(program, "resolution");
         gl.uniform2f(program.resolution, canvas.width, canvas.height);
         
         program.cameraPos = gl.getUniformLocation(program, "cameraPos");
         gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);
         
         program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
         gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);
         
         program.lightDir = gl.getUniformLocation(program, "lightDir");
         // pre normalise light dir
         var x = config.lightDir.x, y = config.lightDir.y, z = config.lightDir.z;
         var len = x*x + y*y + z*z;
         len = 1.0 / Math.sqrt(len);
         gl.uniform3f(program.lightDir, config.lightDir.x*len, config.lightDir.y*len, config.lightDir.z*len);
         
         program.lightColour = gl.getUniformLocation(program, "lightColour");
         gl.uniform3f(program.lightColour, config.lightColour.r, config.lightColour.g, config.lightColour.b);
         
         program.sssColour = gl.getUniformLocation(program, "sssColour");
         gl.uniform3f(program.sssColour, config.sssColour.r, config.sssColour.g, config.sssColour.b);
         
         program.diffuse = gl.getUniformLocation(program, "diffuse");
         gl.uniform3f(program.diffuse, config.surface.diffuse,config.surface.diffuse,config.surface.diffuse);
         
         program.ambientFactor = gl.getUniformLocation(program, "ambientFactor");
         gl.uniform1f(program.ambientFactor, config.surface.ambientFactor);
         
         program.rotateWorld = gl.getUniformLocation(program, "rotateWorld");
         gl.uniform1f(program.rotateWorld, config.global.rotateWorld);
         
         program.moveCamera = gl.getUniformLocation(program, "moveCamera");
         gl.uniform1f(program.moveCamera, config.global.moveCamera);
         
         program.postEffects = gl.getUniformLocation(program, "postEffects");
         gl.uniform1f(program.postEffects, config.global.postEffects);
         
         program.ao = gl.getUniformLocation(program, "ao");
         gl.uniform1f(program.ao, config.global.ao);
         
         program.sss = gl.getUniformLocation(program, "sss");
         gl.uniform1f(program.sss, config.global.sss);
         
         program.shadows = gl.getUniformLocation(program, "shadows");
         gl.uniform1f(program.shadows, config.global.shadows);
         
         program.antialias = gl.getUniformLocation(program, "antialias");
         gl.uniform1f(program.antialias, (config.global.antialias === "Classic"));
         
         program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
         gl.enableVertexAttribArray(program.aVertexPosition);
         gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);
         
         gl.drawArrays(gl.TRIANGLES, 0, numItems);
         
         stats.end();
      }
      requestAnimationFrame(f);
   })();
}
      </script>
      <script src="scripts/utils.js"></script>
      <link rel="stylesheet" href="../../reset.css">
      <link rel="stylesheet" href="./page.css">
      <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
   </head>
   <body onload="init()">
      <canvas id="canvas" width="768" height="512"></canvas>
      <a href="#" id="fullscreen">Toggle Full Screen</a>
      <a href="." id="back">Show me more awesome demos!</a>
   </body>
</html>
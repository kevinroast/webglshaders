<!DOCTYPE html>
<html>
   <head>
<!--
   Ray Marching demo by Kevin Roast
   
   - Lots of code/ideas for this demo come from reading:
     http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
     http://www.pouet.net/topic.php?which=7931&page=1
     http://www.pouet.net/topic.php?which=7920&page=1
     http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
     https://www.shadertoy.com/
   
   If you want to understand this stuff read through the PDF and pouet.net links above!
-->
      <script src="scripts/dat.gui.min.js"></script>
      <script src="scripts/stats.min.js"></script>
      <script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
      <script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 sunDir;
uniform vec3 sunColour;
uniform float ambient;
uniform bool ao;
uniform bool shadows;
uniform bool postEffects;
uniform bool moveCamera;
uniform bool fog;
uniform float fogC;
uniform float fogB;
uniform float param;
uniform sampler2D texture0;

#define GAMMA 0.8
#define CONTRAST 1.2
#define SATURATION 1.4
#define BRIGHTNESS 1.4
#define RAY_DEPTH 150
#define MAX_DEPTH 100.0
#define SHADOW_RAY_DEPTH 48
#define AO_SAMPLES 4
#define FRACT_NOISE_DEPTH 7
#define DISTANCE_MIN 0.002
#define PI 3.14159265

const vec2 delta = vec2(DISTANCE_MIN, 0.);

const vec3 skyColour = vec3(.1, .2, .3);
const vec3 horizColour = vec3(.38, .36, .32);

float NoiseL(in vec2 x)
{
   return texture2D(texture0,(x+vec2(0.5,0.5))/512.0,-100.0).x;
}

float Noise(in vec2 x)
{
   vec2 p = floor(x);
   vec2 f = fract(x);
   f = f*f*(3.0-2.0*f);
   float n = p.x + p.y*57.0;
   float res = mix(mix( texture2D(texture0,(n+vec2(0.5,0.5))/512.0,-100.0).x, texture2D(texture0,(n+1.0+vec2(0.5,0.5))/512.0,-100.0).x,f.x),
                   mix( texture2D(texture0,(n+57.0+vec2(0.5,0.5))/512.0,-100.0).x, texture2D(texture0,(n+58.0+vec2(0.5,0.5))/512.0,-100.0).x,f.x),f.y);
   return res;
}

mat2 octave_m = mat2(2.0,1.4,-1.4,2.0);
#define AMPLITUDE 0.75
#define LACUNARITY 2.333
// ridge offset - see http://thebookofshaders.com/13/
#define OFFSET 1.5
float FractalNoiseM(in vec2 xy)
{
   float a = AMPLITUDE;
   float f = 0.0;
   float n = 0.0;
   for (int i = 0; i < FRACT_NOISE_DEPTH - 2; i++)
   {
      n = Noise(xy);
      n = OFFSET - n;
      f += n * n * a;
      a *= 0.5;
      xy *= octave_m;
   }
   return f;
}

float FractalNoiseH(in vec2 xy)
{
   float a = AMPLITUDE;
   float f = 0.0;
   float n = 0.0;
   for (int i = 0; i < FRACT_NOISE_DEPTH; i++)
   {
      n = Noise(xy);
      n = OFFSET - n;
      f += n * n * a;
      a *= 0.5;
      xy *= octave_m;
   }
   return f;
}

float Terrain(vec2 pos)
{
   return FractalNoiseH(pos.yx*0.571)*0.693 - Noise(cos(pos.yx*0.6)+sin(pos.xy*0.6));
}

float TerrainM(vec2 pos)
{
   return FractalNoiseM(pos.yx*0.571)*0.693 - Noise(cos(pos.yx*0.6)+sin(pos.xy*0.6));
}

// This should return continuous positive values when outside and negative values inside,
// which roughly indicate the distance of the nearest surface.
// NOTE! for increased performance, but lower Shadow/AO/intersect accuracy, change Terrain() to TerrainM() here
float Dist(vec3 pos)
{
   return pos.y - TerrainM(pos.xz);
}

vec3 CalcNormal(in vec3 pos, float t)
{
   // NOTE: subtle changes to this value affect the sharpness of the normal - 0.001 is about same as GetNormal() above
   vec2 eps = vec2(0.001*t, 0.0);
   return normalize( vec3( Terrain(pos.xz-eps.xy) - Terrain(pos.xz+eps.xy),
                           2.0*eps.x,
                           Terrain(pos.xz-eps.yx) - Terrain(pos.xz+eps.yx) ) );
}

// Original method by David Hoskins
vec3 Sky(in vec3 rd)
{
   float sunAmount = max(dot(rd, sunDir), 0.0);
   float v = pow(1.0 - max(rd.y,0.0),6.);
   vec3 sky = mix(skyColour, horizColour, v);
   sky += sunColour * sunAmount * sunAmount * .25 + sunColour * min(pow(sunAmount, 800.0)*1.5, .3);
   
   return clamp(sky, 0.0, 1.0);
}

vec3 DepthFog(in vec3  rgb,in float distance, in vec3 rd, in vec3 ro)
{
   float fogAmount = 0.0;
   if (fog) fogAmount = fogC * exp(-ro.y*fogB) * (1.0-exp(-distance*rd.y*fogB))/rd.y;
   return mix(rgb, Sky(rd), clamp(fogAmount, 0.0, 1.0));
}

// Based on original code by IQ
const float spread = 64.0;
float SoftShadow(vec3 ro, vec3 rd)
{
   float res = 1.0;
   float t = 0.05;    // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
   for (int i=0; i<SHADOW_RAY_DEPTH; i++)
   {
      if (t < 16.0)  // max-t
      {
         float h = Dist(ro + rd * t);
         res = min(res, spread*h/t);
         t += h;
      }
   }
   return clamp(res, 0.0, 1.0);
}

// Based on original code by IQ
float CalcAO(vec3 p, vec3 n)
{
   float r = 0.0;
   float w = 1.0;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float d0 = float(i) * 0.2;
      r += w * (d0 - Dist(p + n * d0));
      w *= 0.5;
   }
   return 1.0 - clamp(r,0.0,1.0);
}

// "Natural light" shading - main sun light with soft shadows, sky light with AO and indirect sun bounce light with AO
// see http://iquilezles.org/www/articles/outdoorslighting/outdoorslighting.htm
const vec3 rockColour = vec3(1.0, 0.8, 0.8);
const vec3 peaksColour = vec3(1.5);
const vec3 grassColour = vec3(0.3, 0.7, 0.3);  //(0.1,0.7,0.2);
const float specularHardness = 64.0;
const float specular = 1.00;
vec3 Shading(vec3 pos, vec3 rd, vec3 norm, vec3 ro)
{
   float sun = clamp(dot(norm, sunDir), 0.0, 1.0);
   vec3 heading = normalize(-rd + sunDir);
   
   // start with rock colour, add in snow on peaks and grass in valleys
   float dif = 0.5;
   float snow = NoiseL(norm.yz*2.111);
   float mixSnow = smoothstep(0.05, 0.4, pos.y - snow + 0.65);
   float grass = NoiseL(norm.xy*0.973) - 0.3;
   
   vec3 colour = rockColour;
   
   // give a more abrubt cut-off where snow is mixed into rock colour
   // snow also has a specular reflective component
   colour = mix(colour, peaksColour, mixSnow);
   float spec = mix(0.0, pow(max(0.0, dot(heading, norm)), specularHardness), mixSnow);
   dif = mix(dif, 0.9, mixSnow);

   // mix grass colour
   float mixGrass = 1.0 - clamp(pos.y + 0.45 + grass, 0.0, 1.0);
   colour = mix(colour, grassColour, mixGrass);
   dif = mix(dif, 0.8, mixGrass);
   
   vec3 light = sun * sunColour * dif + (spec * specular);

   if (shadows) {
      // colorize penumbras
      light *= pow(vec3(SoftShadow(pos, sunDir)), vec3(1.0, 1.2, 1.5));
   }
   
   // sky light with AO
   float occ = ambient;
   if (ao) occ = CalcAO(pos, norm) * ambient;
   float sky = clamp(0.5 + 0.5 * norm.y, 0.0, 1.0);
   light += sky * vec3(0.24, 0.20, 0.24) * occ;
   
   // indirect light with AO
   float ind = clamp(dot(norm, normalize(sunDir * vec3(-1.0,0.0,-1.0))), 0.0, 1.0);
   light += ind * colour * 0.3 * occ;
   
   colour *= light;
   
   // fogging
   colour = DepthFog(colour, length(ro-pos), rd, ro);
   
   return colour;
}

// Original method by David Hoskins
vec3 PostEffects(vec3 rgb, vec2 xy)
{
   rgb = pow(rgb, vec3(GAMMA));
   rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
   rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 ); 
   return rgb;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

void main()
{
   vec3 off = vec3(0.0);
   if (moveCamera) {
      off.z -= time*1.0;
      off.y += cos(time*0.5)*0.5;
   }
   
   // position and ray direction
   vec2 p = gl_FragCoord.xy / resolution.xy;
   vec3 ro = cameraPos + off;
   vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
   
   // march ray
   vec4 res = vec4(0.0);
   float t = 0.0;
   float d = 1.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      vec3 p = ro + rd * t;
      d = Dist(p);
      if (d < DISTANCE_MIN)
      {
         res = vec4(p, 1.0);  // HIT
         break;
      }
      t += d * 0.5;
      if (t > MAX_DEPTH) break;
   }
   
   if (res.a == 0.0)
   {
      res.rgb = Sky(rd);
   }
   else
   {
      res.rgb = Shading(res.rgb, rd, CalcNormal(res.rgb, t), ro).rgb;
   }
   
   if (postEffects) res.rgb = PostEffects(res.rgb, p);
   
   gl_FragColor = vec4(res.rgb, 1.0);
}
      </script>
      
      <script type="text/javascript">
var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
                            window.mozRequestAnimationFrame || window.msRequestAnimationFrame || 
                            function(c) {window.setTimeout(c, 15)};
var config = {
   camera: {
      x: 0.0, y: 2.0, z: 0.0
   },
   lookat: {
      x: 25.0, y: -20.0, z: -65.0
   },
   sunDir: {
      x: -2.0, y: 1.6, z: -4.0
   },
   sunColour: {
      r: 1.7, g: 1.0, b: 0.6
   },
   surface: {
      ambient: 0.2
   },
   global: {
      ao: true,
      shadows: true,
      postEffects: true,
      moveCamera: true,
      fog: true,
      fogB: 0.15,
      fogC: 0.30,
      param: 0.5
   }
};

var pause = false;
var stats = new Stats();
function init()
{
   var img = new Image();
   img.onload = function() {
      loaded([img]);
   };
   img.src = "./noise512.png";
}

var _images;
var aspect, gl;
function loaded(images)
{
   _images = images;
   document.addEventListener('keydown', function(e) {
      switch (e.keyCode)
      {
         case 32:
         case 27: // ESC
            pause = !pause;
            break;
      }
   }, false);

   // add GUI controls
   var mobile = (navigator.userAgent.indexOf("Android") !== -1);
   var gui = new dat.GUI();
   var panel = gui.addFolder('Camera Position');
   panel.add(config.camera, "x").min(-256).max(256).step(0.1);
   panel.add(config.camera, "y").min(-100).max(100).step(0.1);
   panel.add(config.camera, "z").min(-100).max(100).step(0.1);
   //panel.open();
   panel = gui.addFolder('Camera LookAt');
   panel.add(config.lookat, "x").min(-256).max(256).step(0.1);
   panel.add(config.lookat, "y").min(-100).max(100).step(0.1);
   panel.add(config.lookat, "z").min(-100).max(100).step(0.1);
   if (!mobile) panel.open();
   panel = gui.addFolder('Sun Direction');
   panel.add(config.sunDir, "x").min(-10).max(10).step(0.1);
   panel.add(config.sunDir, "y").min(-10).max(10).step(0.1);
   panel.add(config.sunDir, "z").min(-10).max(10).step(0.1);
   if (!mobile) panel.open();
   panel = gui.addFolder('Sun Colour');
   panel.add(config.sunColour, "r").min(0).max(3).step(0.1);
   panel.add(config.sunColour, "g").min(0).max(3).step(0.1);
   panel.add(config.sunColour, "b").min(0).max(3).step(0.1);
   if (!mobile) panel.open();
   panel = gui.addFolder('Surface');
   panel.add(config.surface, "ambient").min(0).max(1).step(0.05);
   //panel.open();
   panel = gui.addFolder('Global');
   panel.add(config.global, "shadows").name("Shadows");
   panel.add(config.global, "ao").name("Ambient Occlusion");
   panel.add(config.global, "postEffects").name("Post Effects");
   panel.add(config.global, "moveCamera").name("Move Camera");
   panel.add(config.global, "fog").name("Fog");
   panel.add(config.global, "fogB").min(0.01).max(1).step(0.01);
   panel.add(config.global, "fogC").min(0.01).max(1).step(0.01);
   panel.add(config.global, "param").min(0.1).max(1.0).step(0.01);
   if (!mobile) panel.open();
   
   document.body.appendChild(stats.domElement);
   
   // create webgl context on the canvas element
   var canvas = document.getElementById("canvas");
   aspect = canvas.width / canvas.height;
   try
   {
      gl = canvas.getContext("experimental-webgl");
   }
   catch (e)
   {
      document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
      return;
   }
   gl.viewport(0, 0, canvas.width, canvas.height);
   gl.clearColor(0, 0, 0, 1);
   gl.clear(gl.COLOR_BUFFER_BIT);
   
   // textures
   var noiseTexture = gl.createTexture();
   gl.bindTexture(gl.TEXTURE_2D, noiseTexture);
   gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, gl.RGBA, gl.UNSIGNED_BYTE, _images[0]);
   gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.LINEAR);
   gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.LINEAR_MIPMAP_NEAREST);
   gl.generateMipmap(gl.TEXTURE_2D);
   gl.bindTexture(gl.TEXTURE_2D, null);
   
   // get the vertex and fragment shader source
   var v = document.getElementById("vertex").firstChild.nodeValue;
   var f = document.getElementById("fragment").firstChild.nodeValue;
   
   // compile and link the shaders
   var vs = gl.createShader(gl.VERTEX_SHADER);
   gl.shaderSource(vs, v);
   gl.compileShader(vs);
   
   var fs = gl.createShader(gl.FRAGMENT_SHADER);
   gl.shaderSource(fs, f);
   gl.compileShader(fs);
   
   var program = gl.createProgram();
   gl.attachShader(program, vs);
   gl.attachShader(program, fs);
   gl.linkProgram(program);
   
   // debug shader compile status
   var error = false;
   if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(vs));
   }
   
   if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(fs));
   }
   
   if (!gl.getProgramParameter(program, gl.LINK_STATUS))
   {
      error = true;
      console.log(gl.getProgramInfoLog(program));
   }
   if (error) return;
   
   var firstTime = Date.now();
   (f = function() {
      if (!pause)
      {
         stats.begin();
         
         // create vertices to fill the canvas with a single quad 
         var vertices = new Float32Array(
            [
               -1, 1*aspect, 1, 1*aspect, 1, -1*aspect,
               -1, 1*aspect, 1, -1*aspect, -1, -1*aspect
            ]);
         
         var vbuffer = gl.createBuffer();
         gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
         gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);
         
         var triCount = 2,
             numItems = vertices.length / triCount;
         
         gl.useProgram(program);
         
         var time = (Date.now() - firstTime) / 1000.0;
         program.time = gl.getUniformLocation(program, "time");
         gl.uniform1f(program.time, time);
         
         program.resolution = gl.getUniformLocation(program, "resolution");
         gl.uniform2f(program.resolution, canvas.width, canvas.height);
         
         program.cameraPos = gl.getUniformLocation(program, "cameraPos");
         gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);
         
         program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
         gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);
         
         program.sunDir = gl.getUniformLocation(program, "sunDir");
         // pre normalise light dir
         var x = config.sunDir.x, y = config.sunDir.y, z = config.sunDir.z;
         var len = x*x + y*y + z*z;
         len = 1.0 / Math.sqrt(len);
         gl.uniform3f(program.sunDir, config.sunDir.x*len, config.sunDir.y*len, config.sunDir.z*len);
         
         program.sunColour = gl.getUniformLocation(program, "sunColour");
         gl.uniform3f(program.sunColour, config.sunColour.r, config.sunColour.g, config.sunColour.b);
         
         program.ambient = gl.getUniformLocation(program, "ambient");
         gl.uniform1f(program.ambient, config.surface.ambient);
         
         program.moveCamera = gl.getUniformLocation(program, "moveCamera");
         gl.uniform1f(program.moveCamera, config.global.moveCamera);
         
         program.postEffects = gl.getUniformLocation(program, "postEffects");
         gl.uniform1f(program.postEffects, config.global.postEffects);
         
         program.ao = gl.getUniformLocation(program, "ao");
         gl.uniform1f(program.ao, config.global.ao);
         
         program.shadows = gl.getUniformLocation(program, "shadows");
         gl.uniform1f(program.shadows, config.global.shadows);
         
         program.fog = gl.getUniformLocation(program, "fog");
         gl.uniform1f(program.fog, config.global.fog);
         
         program.fogB = gl.getUniformLocation(program, "fogB");
         gl.uniform1f(program.fogB, config.global.fogB);
         
         program.fogC = gl.getUniformLocation(program, "fogC");
         gl.uniform1f(program.fogC, config.global.fogC);
         
         program.param = gl.getUniformLocation(program, "param");
         gl.uniform1f(program.param, config.global.param);
         
         gl.bindTexture(gl.TEXTURE_2D, noiseTexture);
         gl.uniform1i(gl.getUniformLocation(program, "texture0"), 0);
         
         program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
         gl.enableVertexAttribArray(program.aVertexPosition);
         gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);
         
         gl.drawArrays(gl.TRIANGLES, 0, numItems);
         
         stats.end();
      }
      requestAnimationFrame(f);
   })();
}
      </script>
      <script src="scripts/utils.js"></script>
      <link rel="stylesheet" href="../../reset.css">
      <link rel="stylesheet" href="./page.css">
      <link href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' rel='stylesheet' type='text/css'>
   </head>
   <body onload="init()">
      <canvas id="canvas" width="768" height="512"></canvas>
      <a href="#" id="fullscreen">Toggle Full Screen</a>
      <a href="." id="back">Show me more awesome demos!</a>
   </body>
</html>